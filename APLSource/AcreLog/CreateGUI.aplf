 n←CreateGUI log;html;caption;posn;size
⍝ Create and populate the GUI
 html←ConvertAcreLogToHTML log
 n←⎕NS''
 caption←((⍕##.OS.GetPID),' Acre Log')
 :If ##.WinRegSimple.Exists RegKey,'\posn'
     posn←⊃(//)⎕VFI ##.WinRegSimple.Read RegKey,'\posn'
     size←⊃(//)⎕VFI ##.WinRegSimple.Read RegKey,'\size'
     n.TheForm←⎕NEW'Form'((('Posn'posn)('Size'size)('Coord' 'Pixel')('Caption'caption)))
 :Else
     n.TheForm←⎕NEW'Form'(('Coord' 'Pixel')('Caption'caption)('Posn'(40 15))('Size'(700 1000)))
 :EndIf
 n.TheForm.⎕WX←3
 n.TheForm.Coord←'Prop'
 n←CreateMenubar n
 n←CreateStatusbar n
 n.Browser←n.TheForm.⎕NEW'HTMLRenderer'(('Posn'(0 0))('Size'(100 100))('AsChild' 1)('Attach'('Top' 'Left' 'Bottom' 'Right')))
 n.(Browser SB).Coord←⊂'Pixel'
 n.Browser.Size[1]-←1+n.SB.Size[1]
 n.TheForm.Coord←'Pixel'
 n.∆Closed←0
 n.TheForm.onClose←'OnClose'
 n.TheForm.on9999←'OnUpdate&'
 n.TheForm.n←n
 ⎕SE.acre.RefToLogGUI←n.TheForm
 ((CR=html)/html)←NL
 n.Browser.HTML←html
 n.∆WSID←⎕WSID
 ⍝Done
